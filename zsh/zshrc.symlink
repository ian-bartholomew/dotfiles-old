# zmodload zsh/zprof
# shortcut to this dotfiles path is $ZSH
export DOTFILES=$HOME/.dotfiles
export ZSH=$DOTFILES

# your project folder that we can `c [tab]` to
export PROJECTS=~/Dev

# set default user
export DEFAULT_USER=`whoami`

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -Uz compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# Load zgen only if a user types a zgen command
zgen () {
	if [[ ! -s ${ZDOTDIR:-${HOME}}/.zgen/zgen.zsh ]]; then
		git clone --recursive https://github.com/tarjoilija/zgen.git ${ZDOTDIR:-${HOME}}/.zgen
	fi
	source ${ZDOTDIR:-${HOME}}/.zgen/zgen.zsh
	zgen "$@"
}

# check if there's no init script
if [[ ! -s ${ZDOTDIR:-${HOME}}/.zgen/init.zsh ]]; then
  echo "Creating a zgen save"

  zgen oh-my-zsh

  # plugins
  zgen oh-my-zsh plugins/aws
  zgen oh-my-zsh plugins/brew
  zgen oh-my-zsh plugins/docker
  zgen oh-my-zsh plugins/git
  # zgen oh-my-zsh plugins/npm
  zgen oh-my-zsh plugins/yarn
  zgen oh-my-zsh plugins/terraform

  zgen load chrissicool/zsh-256color
  zgen load supercrabtree/k

  zgen load zsh-users/zsh-history-substring-search
  zgen load zsh-users/zsh-autosuggestions

  # completions
  zgen load srijanshetty/docker-zsh
  zgen load zsh-users/zsh-completions src
  zgen load lukechilds/zsh-better-npm-completion

  # theme
  # zgen oh-my-zsh themes/agnoster
  zgen load denysdovhan/spaceship-prompt spaceship

  # save all to init script
  zgen save

else
  source ${ZDOTDIR:-${HOME}}/.zgen/init.zsh
fi

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi
